buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.2.0"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id "com.matthewprenger.cursegradle" version "1.2.0"
    id "net.minecrell.licenser" version "0.4.1"
}

def api = new groovy.json.JsonSlurper().parseText(file("${api_token_path}").text)
def keystore = new groovy.json.JsonSlurper().parseText(file("${keystore_path}").text)
def mcVersion = project.mc_version

apply plugin: "net.minecraftforge.gradle"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "net.minecrell.licenser"
apply plugin: "eclipse"

import net.minecraftforge.gradle.common.task.SignJar

version = project.version
group = "stevekung.mods.ytchat"
archivesBaseName = "YouTubeChat-" + mcVersion
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

minecraft {
    mappings channel: "snapshot", version: "${mcp_mapping}"
    accessTransformer = file("$rootDir/libs/accesstransformer.cfg")

    runs {
        client = {
            workingDirectory project.file("run").canonicalPath
            source sourceSets.main

            mods {
                youtube_chat {
                    source sourceSets.main
                }
            }
        }
        server = {
            workingDirectory project.file("run").canonicalPath
            source sourceSets.main

            mods {
                youtube_chat {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    // Curse Gradle
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"
    compile files("libs/SteveKunG's-Lib-1.13.2-2.0.0-deobf.jar")
    compile fileTree(dir: "$rootDir/.dependencies", include: "*.jar")
}

license {
    header = file("LICENSE-header.txt")
    include "stevekung/mods/ytchat/"

    tasks {
        main {
            files = files("$rootDir/src/main/java")
        }
    }
}

shadowJar {
    relocate "org.apache.commons.codec", "stevekung.mods.ytchat.relocate.org.apache.commons.codec"
    relocate "org.apache.commons.logging", "stevekung.mods.ytchat.relocate.org.apache.commons.logging"
    relocate "org.apache.http", "stevekung.mods.ytchat.relocate.org.apache.http"
    relocate "org.codehaus", "stevekung.mods.ytchat.relocate.org.codehaus"
    relocate "org.mortbay", "stevekung.mods.ytchat.relocate.org.mortbay"
    relocate "org.checkerframework", "stevekung.mods.ytchat.relocate.org.checkerframework"
    relocate "javax.annotation", "stevekung.mods.ytchat.relocate.javax.annotation"
    relocate "javax.servlet", "stevekung.mods.ytchat.relocate.javax.servlet"
    relocate "com.google.api", "stevekung.mods.ytchat.relocate.com.google.api"
    relocate "com.google.common", "stevekung.mods.ytchat.relocate.com.google.common"
    relocate "com.google.errorprone", "stevekung.mods.ytchat.relocate.com.google.errorprone"
    relocate "com.google.j2objc", "stevekung.mods.ytchat.relocate.com.google.j2objc"
    relocate "com.google.thirdparty", "stevekung.mods.ytchat.relocate.com.google.thirdparty"
    relocate "com.fasterxml", "stevekung.mods.ytchat.relocate.com.fasterxml"
    relocate "io.grpc", "stevekung.mods.ytchat.relocate.io.grpc"
    relocate "io.opencensus", "stevekung.mods.ytchat.relocate.io.opencensus"

    classifier ""
    dependencies {
        include(dependency(fileTree(dir: "$rootDir/.dependencies", include: "*.jar")))
    }
    exclude "stevekung/mods/stevekunglib/**"
    exclude "META-INF/stevekunglib_at.cfg"
    exclude "META-INF/maven/**"
    exclude "dummyThing"
    exclude "mozilla/public-suffix-list.txt"
}

reobf {
    shadowJar {}
}

jar {
    from configurations.embed.collect {
        exclude "META-INF/LICENSE*"
        exclude "META-INF/NOTICE*"
        it.isDirectory() ? it : zipTree(it)
    }

    manifest {
        attributes([
                "Specification-Title": "youtube_chat",
                "Specification-Vendor": "SteveKunG",
                "Specification-Version": "1",
                "Implementation-Title": "YouTube-Chat",
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"SteveKunG",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

curseforge {
    apiKey = api.youtube_chat
    project {
        id = "302916"
        changelog = "Change Log"
        changelogType = "markdown"
        releaseType = "release"
        addGameVersion mcVersion

        mainArtifact(file("build/libs/YouTubeChat-" + mcVersion + "-" + version + ".jar")) {
            displayName = "YouTubeChat-" + mcVersion + "-" + version
            relations {
                requiredLibrary "stevekungs-lib"
            }
        }
    }
}

task signJar(type: SignJar, dependsOn: jar) {
    keyStore = keystore.keystore_path
    alias = keystore.alias
    storePass = keystore.keystore_pass
    keyPass = keystore.keystore_pass
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar